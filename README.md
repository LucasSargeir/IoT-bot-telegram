# TuledBOT
**Alunos:** Lucas Sargeiro Gomes de Mello; Marcelo Henrique Untersander Pereira de Barros.



### Projeto

O TuledBOT √© um projeto criado para a disciplina de IoT do CEFET-RJ. O projeto consistem em um BOT (rob√¥)  para o [Telegram](https://telegram.org) que se comunica com o ESP8266 e serve como interface para acender e apagar um led, e buscar informa√ß√µes de temperatura e humidade.



### Componentes

Os componentes utilizados no projeto foram:

- Led vermelho;
- Sensor DHT11 (temperatura e umidade);
- ESP8266;
- Jumpers;
- Resistores diversos;



### Conex√£o dos componentes

A conex√£o dos componentes foi colocada como apresentada na imagem abaixo:

![foto_1](colocar foto).



explica√ß√£o...



### Arquitetura

Com o objetivo de separar as responsabilidades e manter o c√≥digo organizado o programa foi divido em alguns m√≥dulos, sendo eles:

- **tuledbot:** M√≥dulo principal, onde todo o programa √© centralizado. Nesse m√≥dulo temos apenas os *setups* e *loops* de cada m√≥dulo;
- **_pinout:** M√≥dulo respons√°vel pelo mapeamento dos pinos;
- **wifi:** M√≥dulo respons√°vel pela conex√£o WiFi. Esse m√≥dulo implementa o Wif Manager para que os dados de conex√£o sejam inseridos pelo usu√°rio;
- **ota:** M√≥dulo respons√°vel pelas atualiza√ß√µes do programa pela porta de rede;
- **dht:** M√≥dulo respons√°vel por fazer a leitura de umidade e temperatura;
- **led:** M√≥dulo respons√°vel por acender e apagar o led;
- **telegram:** M√≥dulo respons√°vel por realizar a comunica√ß√£o entre o *Telegram* e o ESP8266 realizando as tarefas solicitadas.



Cada m√≥dulo expo√µe aos outros as fun√ß√µes necess√°rias para realizar cada uma das a√ß√µes que podem ser solicitadas no *Telegram* ou para configurar o ESP. Foi definido que, para cada m√≥dulo, quando necess√°rio, teria uma fun√ß√£o com o sufixo *Init*, que seriam incluidas ao *setup()* do projeto e uma fun√ß√£o com o sufixo *Loop* que seriam incluidas no *loop()* principal do projeto.





## Implementa√ß√£o

A implementa√ß√£o do programa foi feita atrav√©s da IDE do Arduino e foi necess√°rio a instal√ß√£o de algumas bibliotecas, sendo elas:

![wifi](./images/wifi.png)![telegram](./images/telegram.png)![dht](./images/dht.png)





### M√≥dulo TuledBOT

O m√≥dulo **Tuledbot**  √© composto pelo arquivo`tuledbot.ino` . Esse m√≥dulo tem o objetivo de centralizar todo os *setups* e *loops* necess√°rios para a execu√ß√£o do ESP.  Nele s√£o importados as fun√ß√µes expostas pelos outros m√≥dulos para inicializa√ß√£o e execu√ß√£o e, quando necess√°rio, s√£o definidos os pinos que ser√£o executados em cada m√≥dulo.

```cpp
#include <ESP8266WiFi.h>

void serialInit();
void wifiInit(bool resetSettings);
void otaInit();
void dhtInit();

void wifiLoop();
void otaLoop();
void dhtLoop();

void setup() {
    serialInit();
    wifiInit(false);
    otaInit();
    ledInit(D0);
    dhtInit(D1);
    telegramInit();
}

void loop() {
    wifiLoop();
    otaLoop();
    telegramLoop();
}

void serialInit(){
    Serial.begin(115200);
}
```



### M√≥dulo Pinout

O m√≥dulo **Pinout** √© composto pelo arquivo `_pinout.ino`. Esse m√≥dulo foi criado com o objetivo de mapear os pinos do ESP, nele tamb√©m s√£o comentados os pinos utilizados por cada m√≥dulo, apenas para registro.

```cpp
#include <Arduino.h>

#define D0    16          // PINO DO LED
#define D1    5           // PINO DO DHT
#define D2    4 
#define D3    0 
#define D4    2 
#define D5    14  
#define D6    12 
#define D7    13 
#define D8    15 
#define D9    3  
#define D10   1  
```

> A biblioteca *Arduino.h* deve ser importada para garantir que no momento da compila√ß√£o esse outro arquivo tamb√©m ser√° analisado.



### M√≥dulo WiFi

O modulo **WiFi** √© composto pelo arquivo `wifi.ino`. Esse m√≥dulo foi criado com o objetivo de configurar as credencias de rede, sem precisar inserir essas no c√≥digo. Quando o ESP √© ligado pela primeira vez, ele entra no modo *acess point*, e uma vez nesse modo, atrav√©s de qualquer dispositivo com conex√£o WiFi, voc√™ poder√° encontrar a rede *TuledBot_Config*.

Para configurar os dados de acesso basta se conectar nessa rede com a senha *"senha123"* e voc√™ ser√° direcionado a p√°gina de configura√ß√£o do acesso. Caso voc√™ n√£o seja direcionado automaticamente, procure pelo bot√£o que leva at√© a p√°gina de configura√ß√£o da rede. O passo a passo para configurar pode ser visto abaixo:

<img alt="conexao parte 1" src="./images/connect_1.png" width="33%"/><img alt="conexao parte 2" src="./images/connect_2.png" width="33%"/><img alt="conexao parte 3" src="./images/connect_3.png" width="33%"/>



```cpp
#include <Arduino.h>
#include <WiFiManager.h>
#include <ESP8266WebServer.h>

const char* WIFI_HOSTNAME = "Tuledbot";
const char* WIFI_SSID = "TuledBot_Config";
const char* WIFI_PASSWORD = "senha123";

void wifiManagerConfig(bool resetSettings);

void wifiInit(bool resetSettings) {
    WiFi.hostname(WIFI_HOSTNAME);
    wifiManagerConfig(resetSettings);
}

void wifiManagerConfig(bool resetSettings) {
    Serial.println("\n--- Starting wifi configuration ---");
    WiFiManager wifiManager;
    
    if (resetSettings) {
        Serial.println("Reseting credentials...");
        wifiManager.resetSettings();
    }
    
    wifiManager.autoConnect(WIFI_SSID, WIFI_PASSWORD);
    Serial.println("--- Finished wifi configuration ---");
}

void wifiLoop() {
    
    if (WiFi.status() == WL_CONNECTED){
        return;
    }

    Serial.println("\n--- Starting WiFi Connection ---");

    WiFi.hostname(WIFI_HOSTNAME);
    WiFi.begin();

    while (WiFi.status() != WL_CONNECTED) {
        delay(100);
        Serial.println(".");
    }
   
    Serial.print("Conectado com sucesso na rede: ");
    Serial.println(WIFI_SSID);

    Serial.println("--- End WiFi Connection ---");
}
```

> A biblioteca *Arduino.h* deve ser importada para garantir que no momento da compila√ß√£o esse outro arquivo tamb√©m ser√° analisado.



Esse m√≥dulo expo·∫Ωs 3 m√©todos para os demais m√≥dulos, s√£o eles:

-  `wifiInit(bool resetSettings)`: Esse √© o m√©todo respons√°vel por iniciar o servi√ßo do WiFi Manager. O m√©todo recebe um par√¢metro que indica se as configura√ß√µes salvas devem ser  utilizadas ou se os dados devem ser excluidos;

- `wifiManagerConfig(bool resetSettings)` : Esse m√©todo √© utilizado para realizar a conex√£o com o Wifi. O m√©todo recebe um par√¢metro que indica se a conex√£o ser√° realizada com os dados salvos ou se novos dados devem ser incluidos. Em geral esse m√©todo n√£o precisa ser chamado por outros m√≥dulos j√° que o pr√≥prio m√©todo de *setup* utiliza ele, por√©m, caso seja desejado resetar as configura√ß√µes de acesso durante a execu√ß√£o do programa ele pode ser utilizado;

- `WifiLoop()`: Esse m√©todo √© utilizado para manter a conex√£o ativa. O m√©todo verifica o estado da conex√£o, e caso tenha sido interrompida, ele tenta reconectar. Esse m√©todo deve ser chamado dentro do *loop* principal do programa para realizar as reconex√µes.

  



### M√≥dulo OTA

O m√≥dulo **OTA** √© composto pelo arquivo `ota.ino`.  Esse m√≥dulo foi criado com o objetivo de permiti que o ESP tivesse seu *firmware* atualizado sem a necessidade de ter ele conectado a um computador pelo cabo. O OTA permite que, se ambos os equipamentos estiverem conectados na mesma rede, o bin√°rio possa ser carregado atrav√©s de uma porta de rede.

Para o usu√°rio o OTA √© transparente, sem a necessidade de quaisquer intera√ß√£o para atualiza√ß√£o, e totalmente seguro, j√° que √© necess√°rio uma senha para atualiza√ß√£o do *firmware*.

```cpp
#include <Arduino.h>
#include <ArduinoOTA.h>

const char* OTA_HOSTNAME = "TuledBot_OTA";
const char* OTA_SSID = "TuledBot_OTA";
const char* OTA_PASSWORD = "senha";

void otaOnStart();
void otaOnProgress(unsigned int progress, unsigned int total);
void otaOnEnd();
void otaOnError(ota_error_t error);

void otaInit() {
    ArduinoOTA.setHostname(OTA_HOSTNAME);
    ArduinoOTA.setPassword(OTA_PASSWORD);
    
    ArduinoOTA.onStart(otaOnStart);
    ArduinoOTA.onEnd(otaOnEnd);
    ArduinoOTA.onProgress(otaOnProgress);
    ArduinoOTA.onError(otaOnError);

    ArduinoOTA.begin();
}

void otaLoop() {
    ArduinoOTA.handle();
}

void otaOnStart() {
    Serial.println("\n--- OTA Start ---");
}

void otaOnProgress(unsigned int progress, unsigned int total) {
    Serial.printf("Progress: %u%%\r", (progress / (total / 100)));
}

void otaOnEnd() {
    Serial.println("--- OTA End ---");
}

void otaOnError(ota_error_t error) {
    Serial.printf("Error[%u]: ", error);
    
    if(error == OTA_AUTH_ERROR){
        Serial.println("Auth Failed");    
    } 
    else if(error == OTA_BEGIN_ERROR){
        Serial.println("Begin Failed");
    }
    else if(error == OTA_CONNECT_ERROR){
        Serial.println("Connect Failed");
    }
    else if(error == OTA_RECEIVE_ERROR){
        Serial.println("Receive Failed");
    }
    else if(error == OTA_END_ERROR){
        Serial.println("End Failed");
    }
}
```

> A biblioteca *Arduino.h* deve ser importada para garantir que no momento da compila√ß√£o esse outro arquivo tamb√©m ser√° analisado.



ara fim de apresenta√ß√£o colocamos a senha *"senha"* para atualiza√ß√£o, mas por quest√µes de seguran√ßa √© necess√°rio colocar uma senha mais segura. Esse m√≥dulo exp√µes aos demais m√≥dulos as fun√ß√µes:

- `otaInit()`: Esse m√©todo √© respons√°vel por configurar o OTA. Nesse m√©todo s√£o configurados os *hotname* e senha, para atualiza√ß√£o do programa, al√©m das fun√ß√µes de *callback* para cada momento da execu√ß√£o do OTA;
- `otaLoop()`: Esse m√©todo √© respons√°vel por manter o OTA em execu√ß√£o, para que isso seja poss√≠vel √© necess√°rio que esse m√©todo seja chamado dentro do*loop* principal do programa;
- `otaOnStart()`:  Usando como *callback* quando o OTA √© iniciado. Esse m√©todo √© utilizado para sabermos que o m√≥dulo iniciou sem nenhum problema;
- `otaOnProgress()`: Usando como *callback* quando o OTA est√° subindo as atualiza√ß√µes. Esse m√©todo √© utilizado para sabermos o progresso do evio;
- `otaOnEnd()`: Usando como *callback* quando o OTA √© finalizado. Esse m√©todo √© utilizado para sabermos caso o m√≥dulo seja finalizado;

- `otaOnError()`: Usando como *callback* quando o OTA acaba encontrando algum erro. Esse m√©todo √© utilizado para n√£o nos depararmos com um erro sem saber.





### M√≥dulo DHT

O m√≥dulo **DHT** √© composto pelo arquivo `dht.ino`. Esse m√≥dulo √© respons√°vel por realizar as medi√ß√µes de temperatura e umidade do ambiente. Para realiza√ß√£o das medi√ß√µes foi utilizado o sensor DHT11. Embora esse sensor tenha apresentado dificuldade para conectar, depois de muita tentativa ele serviu bem.

<img alt="DHT11" src="https://www.baudaeletronica.com.br/media/catalog/product/cache/1/image/800x/9df78eab33525d08d6e5fb8d27136e95/d/h/dht-11.png" width="8%"/> **Sensor DHT11**

```cpp
#include <Arduino.h>
#include <DHT.h>

DHT* dht;

void dhtPrintMessage(float humidity, float temperature);

void dhtInit(int PIN) {
    dht = new DHT(PIN, DHT11);
  
    Serial.println("--- Starting DHT ---");
    (*dht).begin();
}


void dhtLoop() {
    float humidity    = (*dht).readHumidity();
    float temperature = (*dht).readTemperature();
    
    if (isnan(temperature) || isnan(humidity)) {
        Serial.println("--- Failed to read from DHT ---");
    }
    else {
        dhtPrintMessage(humidity, temperature);
    }

    delay(2000);
}

float dhtGetTemperature() {
    return (*dht).readTemperature();
}

float dhtGetHumidity() {
    return (*dht).readHumidity();
}

void dhtPrintTemperatureAndHumidityStatus(float humidity, float temperature) {
    Serial.print("--- Umidade: ");
    Serial.print(humidity);
    Serial.println(" %t       ---");
    Serial.print("--- Temperatura: ");
    Serial.print(temperature);
    Serial.println(" *C   ---");
}
```

> A biblioteca *Arduino.h* deve ser importada para garantir que no momento da compila√ß√£o esse outro arquivo tamb√©m ser√° analisado.



O m√≥dulo criado expo√µe  5 m√©todos para os demais:

- `dhtInit(int PIN)`: Esse m√©todo √© respons√°vel por iniciar o DHT11. O m√©todo √© utilizado para iniciar o sensor, e para isso, ele recebe um par√¢metro que define qual ser√° o pino respons√°vel por trocar dados do sensor;

- `dhtLoop()`: Esse m√©todo √© respons√°vel por realizar a leitura de temperatura e umidade  a cada 2 segundos e mostrar os valores no monitor serial. Para que esse m√©todo execute como esperado ele deve ser incluido no *loop* principal do programa;

- `dhtGetTemperature()`: Esse m√©todo serve para realizar a leitura da temperatura e retornar esse valor;

- `dhtGetHumidity()`: Esse m√©todo serve para realizar a leitura da umidade e retornar esse valor;

- `dhtPrintTemperatureAndHumidityStatus(float humidity, float temperature)`: Esse m√©todo √© utilizado para mostrar valores de determinada temperatura e umidade no monitor serial.





### M√≥dulo Led

O m√≥dulo **Led** √© composto pelo arquivo `led.ino`. Esse m√≥dulo √© respons√°vel por controlar o led incluido no projeto. Para o nosso trabalho foi utilizado um led vermelho. Para acender o led √© necess√°rio acionar o valor *HIGH* no pino que o led est√° conectado, e para apagar, basta acionar o valor *LOW*.

<img alt="led vermelho" src="https://www.baudaeletronica.com.br/media/catalog/product/cache/1/image/9df78eab33525d08d6e5fb8d27136e95/l/e/led_red_1.jpg" width="10%"> **Led**

```cpp
#include <Arduino.h>

int PIN_LED;
int led_status = 0;

void ledInit(int PIN){
    PIN_LED = PIN;
  
    pinMode(PIN_LED, OUTPUT);
    digitalWrite(PIN_LED, LOW);
}

void ledTurnOn(){
  Serial.println("--- Led Aceso               ---");
  digitalWrite(PIN_LED, HIGH);
  led_status = 1;
}


void ledTurnOff(){
  Serial.println("--- Led Apagado             ---");
  digitalWrite(PIN_LED, LOW);
  led_status = 0;
}

int getLedStatus() {
    return led_status;
}

void ledBlink(int time){
    Serial.println("--- Piscando Led            ---");
    
    ledTurnOn();
    delay(time);
    ledTurnOff();
}
```

> A biblioteca *Arduino.h* deve ser importada para garantir que no momento da compila√ß√£o esse outro arquivo tamb√©m ser√° analisado.



O m√≥dulo Led exp√µe 5 m√©todos para os demais m√≥dulos:

- `ledInit(int PIN)`: Esse m√©todo √© respons√°vel por iniciar led. Esse m√©todo recebe como par√¢metro o pino que ser√° utilizado para trocar informa√ß√µes com o led;

- `ledLoopOn()`: Esse m√©todo √© respons√°vel por acender o led atualizando o estado atual dele. Al√©m disso o m√©todo nos avisa pelo monitor serial que o led acendeu;

- `ledTurnOff()`: Esse m√©todo √© respons√°vel por apagar o led atualizando o estado atual dele. Al√©m disso o m√©todo nos avisa pelo monitor serial que o led apagou;

- `ledTurnStatus()`: Esse m√©todo √© respons√°vel por indicar o estado atual do led, 1 caso esteja aceso e 0 se estiver apagado;

- `ledBlink(int time)`: Esse m√©todo √© respons√°vel por piscar o led. O m√©todo recebe como par√¢metro um tempo em milissegundos, que representa quanto tempo o led ficar√° aceso. Al√©m disso o m√©todo nos avisa pelo monitor serial que o led piscou.





### M√≥dulo Telegram

O m√≥dulo **Telegram** √© composto pelo arquivo `telegram.ino`. Esse m√≥dulo √© respons√°vel por conectar o ESP ao *Telegram* atrav√©s de um BOT. O pr√≥prio Telegran possui uma [API](https://core.telegram.org/bots) criada para constru√ß√£o de BOTs . Em resumo, voc√™ cadastra um BOT utilizando um outro BOT do Telegram, chamado *The Botfather*, e ele libera um *token* de acesso a API deles.

Para constru√ß√£o do nosso BOT, definimos alguns comandos que s√£o apresentados para o usu√°rio assim que ele entra em contato com BOT:

![chat tuledbot](./images/start.png)



Os comandos podem ser acionados tanto pelo teclado, quando a pessoa digita, quanto clicando nos comando enviados no come√ßo da conversa (destacados em azul na mensagem).

Para a constru√ß√£o do m√≥dulo foi necess√°rios definirmos algumas vari√°veis que s√£o utilizadas para se conectar com a API do *Telegram*, como o *token* recebido pelo *Bot Father*. Al√©m disso, foi necess√°rio importar o certificado SSL do *Telegram* e criar um cliente seguro para se conectar a API. Uma vez feito isso tudo, podemos criar a inst√¢ncia do nosso BOT.

Agora que temos a inst√¢ncia do BOT falta apenas sincronizarmos nosso rel√≥gio com o UTC e j√° poderemos realizar a comunica√ß√£o.

```cpp
#include <Arduino.h>
#include <UniversalTelegramBot.h>


#define BOT_TOKEN "2040806579:AAFrN7RYxL6h260BjmduyIgzJEtwhUg00RY"

unsigned long bot_last_scan_time;
const unsigned long BOT_SCAN_INTERVAL = 100;


X509List cert(TELEGRAM_CERTIFICATE_ROOT);
WiFiClientSecure wifi_secure_client;
UniversalTelegramBot bot(BOT_TOKEN, wifi_secure_client);


void setupUTCTime();
void setupTelegramCommandsDescription();




void telegramInit() {
    setupUTCTime();
    setupTelegramCommandsDescription();
}



void telegramLoop() {
    if(millis() - bot_last_scan_time < BOT_SCAN_INTERVAL) return;
    
    int totalNewMessages = bot.getUpdates(bot.last_message_received + 1);
    
    while (totalNewMessages){
        Serial.print("Nova solicita√ß√£o: ");
        handleNewMessages(totalNewMessages);
        totalNewMessages = bot.getUpdates(bot.last_message_received + 1);
    }
    
    bot_last_scan_time = millis();
}




void setupTelegramCommandsDescription() {
    String commands ="[";
    commands += "          {\"command\":\"led_on\",     \"description\":\"Liga o LED\"},";
    commands += "          {\"command\":\"led_off\",    \"description\":\"Desliga o LED\"},";
    commands += "          {\"command\":\"led_blink\",    \"description\":\"Pisca o LED\"},";
    commands += "          {\"command\":\"led_status\", \"description\":\"Status do LED\"},";
    commands += "          {\"command\":\"temperature\",\"description\":\"Retorna a temperatura do ambiente\"},";
    commands += "          {\"command\":\"humidity\",   \"description\":\"Retorna a umidade do ambiente\"},";
    commands += "          {\"command\":\"th\",         \"description\":\"Retorna a temperatura e a umidade do ambiente\"},";
    commands += "          {\"command\":\"help\",       \"description\":\"Lista os possiveis comandos\"}";
    commands += "     ]";
    bot.setMyCommands(commands);
}



void setupUTCTime() {
    Serial.println("--- configurtando UTC ---");
    
    configTime(0, 0, "pool.ntp.org");
    wifi_secure_client.setTrustAnchors(&cert); // Root certificate for api.telegram.org

    // Sync NTP/Time
    time_t now = time(nullptr);
    while (now < 24 * 3600){
      delay(100);
      now = time(nullptr);
    }
    Serial.println("--- UTC configurado com sucesso ---");
}



/* ---------------------------------- MESSAGE CALLBACKS ------------------------------------ */

void led_on_and_notify(String chat_id) {
    ledTurnOn();
    bot.sendMessage(chat_id, "Led *LIGADO*", "Markdown");
}



void led_off_and_notify(String chat_id) {
    ledTurnOff();
    bot.sendMessage(chat_id, "Led *DESLIGADO*", "Markdown");
}

void led_blink_and_notify(String chat_id){
    ledBlink(10);
    bot.sendMessage(chat_id, "Led *PISCOU*", "Markdown");
}



void led_status_and_notify(String chat_id) {
    int ledStatus = getLedStatus();
    if(ledStatus){
        bot.sendMessage(chat_id, "Led *LIGADO*", "Markdown");
    }else{
        bot.sendMessage(chat_id, "Led *DESLIGADO*", "Markdown");    
    }
}



void temperature(String chat_id) {
    float temperature = dhtGetTemperature();
    String message = "Temperatura atual: " + (String) temperature;
    
    bot.sendMessage(chat_id, message, "");
}



void humidity(String chat_id) {
    float humidity = dhtGetHumidity();
    String message = "Umidade atual: " + (String) humidity;
    
    bot.sendMessage(chat_id, message, "");
}



void th(String chat_id){
    float temperature = dhtGetTemperature();
    float humidity = dhtGetHumidity();

    String message = "Temperatura: " + (String) temperature + "\n" + "Umidade: " + (String) humidity;
    bot.sendMessage(chat_id, message, "");
}



void help(String chat_id, String username){
    String welcome = "Bem vindo ao Tuledbot " + username + " üòÅ. Aqui voc√™ pode acender e apagar leds, e al√©m disso, ficar por dentro da temperatura e umidade do seu ambiente!\n\n";
    welcome += "Utilize os comandos abaixo para as a√ß√µes:\n\n";
    welcome += "*/led_on*: Acender o led\n";
    welcome += "*/led_off*: Apagar o led\n";
    welcome += "*/led_blink*: Piscar o led\n";
    welcome += "*/led_status*: Verificar o status do led\n";
    welcome += "*/temperature*: Verificar a temperatura do ambiente\n";
    welcome += "*/humidity*: Verificar a umidade do ambiente\n";
    welcome += "*/th*: Verificar a temperatura e a umidade do ambiente\n";
    welcome += "*/help*: Listar comandos\n";
    bot.sendMessage(chat_id, welcome, "Markdown");
}



void handleNewMessages(int numNewMessages){
    for (int i = 0; i < numNewMessages; i++){
        String chat_id = bot.messages[i].chat_id;
        String text = bot.messages[i].text;
        String from_name = bot.messages[i].from_name;
    
        Serial.println(text);
        
        if (from_name == ""){
          from_name = "desconhecido";
        }
    
        if (text == "/led_on") led_on_and_notify(chat_id);
        if (text == "/led_off") led_off_and_notify(chat_id);
        if (text == "/led_blink") led_blink_and_notify(chat_id);
        if (text == "/led_status") led_status_and_notify(chat_id);
        if (text == "/temperature") temperature(chat_id);
        if (text == "/humidity") humidity(chat_id);
        if (text == "/th") th(chat_id);
        if (text == "/help" || text == "/start") help(chat_id, from_name);
    }
}
```

> A biblioteca *Arduino.h* deve ser importada para garantir que no momento da compila√ß√£o esse outro arquivo tamb√©m ser√° analisado.



Esse m√≥dulo exp√µe 12 m√©todos para os demais m√≥dulos:

- `telegramInit()`: Esse m√©todo √© respon√°vel por inicial o BOT. O m√©todo inicia as configura√ß√µes necess√°rias para nos comunicarmos com o *Telegram* e inicializa as op√ß√µes de comando junto com uma leve descri√ß√£o;
- `telegramLoop()`: Esse m√©todo √© respons√°vel por manter a conex√£o com o Telegram ativa. A todo momento ele verifica se existe alguma mensagem pendente que o BOT n√£o tenha visto ainda, e caso tenha, manda aquela mensagem para ser resolvida. Para n√£o precisamos buscar por novas mensagens a cada milissegundo, podemos definir um tempo de busca por novas mensagens e s√≥ verificar ap√≥s aquele tempo;
- `setupTelegramCommandsDescription()`: Esse m√©todo √© respons√°vel por atualizar os comandos poss√≠veis para se utilizar no BOT, junto com uma leve descri√ß√£o. Essa descri√ß√£o dos comandos serve para que o *Telegram* possa sugerir as op√ß√µes ao usu√°rio<sup>*</sup>;
- `setupUTCTime()`: Esse m√©todo √© respons√°vel por buscar o hor√°rio UTC em um servidor NTP e sincronicar o rel√≥gio do ESP nesse hor√°rio. Isso √© feito para que as requisi√ß√µes possam acontecer sem problemas;
- `led_on_and_notify(String chat_id)`: Esse m√©todo √© respons√°vel por acender o led e notificar a determinado usu√°rio que isso aconteceu;
- `led_off_and_notify(String chat_id)`: Esse m√©todo √© respons√°vel por apagar o led e notificar a determinado usu√°rio que isso aconteceu;
- `led_blink_and_notify(String chat_id)`: Esse m√©todo √© respons√°vel por piscar o led (com um tempo de 10 ms.) e notificar a determinado usu√°rio que isso aconteceu;
- `led_status_and_notify(String chat_id)`: Esse m√©todo √© respons√°vel por buscar o estado do led e notificar a determinado usu√°rio esse estado;
- `temperature(String chat_id)`: Esse m√©todo √© respons√°vel por buscar a temperatura do ambiente e notificar a determinado usu√°rio essa temperatura;
- `humidity(String chat_id)`: Esse m√©todo √© respons√°vel por buscar a umidade do ambiente e notificar a determinado usu√°rio essa umidade;
- `th(String chat_id)`: Esse m√©todo √© respons√°vel por buscar a temperatura e a umidade do ambiente e notificar a determinado usu√°rio os valores encontrados;
- `help(String chat_id, String username)`: Esse m√©todo √© respons√°vel por notificar certo usu√°rio as op√ß√µes dispon√≠veis para comandos, esse m√©todo chama o usu√°rio pelo nome caso ele tenha um nome p√∫blico no *Telegram*;
- `handleNewMessages(int numNewMessages)`: Esse m√©todo √© respons√°vel por lidar com as requisi√ß√µes dos usu√°rios. Sempre que um usu√°rio envia um pedido, essa fun√ß√£o identifica o pedido e chama a fun√ß√£o respons√°vel por aquela a√ß√£o passando sempre o identificador do usu√°rio que enviou a mensagem, para quele possa ser avisado ap√≥s a conclus√£o da tarefa. Essa fun√ß√£o recebe como par√¢metro o n√∫mero de mensagens n√£o lidas para realizar todas as tarefas pendentes.



<sup>*</sup> As op√ß√µes definidas s√£o apresentadas sempre que um usu√°rio digita o caractere "/" ou quando clica no bot√£o dispon√≠vel para as op√ß√µes. A imagem abaixo mostra o bot√£o dispon√≠vel e como as op√ß√µes s√£o oferecidas.

<img alt="botao de comandos" src="./images/botao.png" width="45%"/><img alt="botao de comandos" src="./images/opcoes.png" width="45%"/>
